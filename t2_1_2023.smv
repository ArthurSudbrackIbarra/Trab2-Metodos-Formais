---------------------------------------------------------------------------
-- Trabalho 2 Metodos Formais.                                           --
---------------------------------------------------------------------------

---------------------------------------------------------------------------
-- TEMPORIZADOR.                                                         --
---------------------------------------------------------------------------

MODULE temporizador(reset)
  VAR 
    tempo : 0 .. 3;
  ASSIGN
    init(tempo) := 0;
    next(tempo) := 
      case
        reset : 0;
        tempo = 3 : 3;
        TRUE : tempo + 1;
      esac;
  DEFINE
    timeout := tempo = 3;

---------------------------------------------------------------------------
-- CONTROLADOR                                                           --
---------------------------------------------------------------------------

MODULE main
  VAR
    veiculos_no_oeste : boolean;
    veiculos_no_leste : boolean;
    veiculos_sobre_ponte : boolean;
    sinaleira_oeste : {verde, vermelho};
    sinaleira_leste : {verde, vermelho};
    vez_sinaleira : {oeste, leste};
    reiniciar_temp : boolean;
    temp : temporizador(reiniciar_temp);
  ASSIGN
    -- Inicialização.
    init(veiculos_no_oeste) := FALSE;
    init(veiculos_no_leste) := FALSE;
    init(veiculos_sobre_ponte) := FALSE;
    init(sinaleira_oeste) := vermelho;
    init(sinaleira_leste) := vermelho;
    init(vez_sinaleira) := oeste;
    init(reiniciar_temp) := FALSE;

    -- Transições de estado.

    -- Reseta temporizador se necessário.
    next(reiniciar_temp) := temp.timeout;

    -- Controla qual sinaleira tem direito de ficar verde.
    next(vez_sinaleira) :=
      case
        !temp.timeout : vez_sinaleira;
        vez_sinaleira = oeste : leste;
        TRUE : oeste;
      esac;

    -- Controla a sinaleira oeste.
    next(sinaleira_oeste) :=
      case
        -- Se deu timeout e era verde, fica vermelha.
        temp.timeout & sinaleira_oeste = verde : vermelho;
        -- Se não tem veículos no oeste, fica vermelha.
        !veiculos_no_oeste : vermelho;
        -- Se tem veículos no oeste e nenhum sobre a ponte, fica verde.
        -- Contanto que seja a vez dessa sinaleira.
        vez_sinaleira = oeste & veiculos_no_oeste & !veiculos_sobre_ponte : verde;
        -- Default.
        TRUE : sinaleira_oeste;
      esac;

    -- Controla a sinaleira leste.
    next(sinaleira_leste) :=
      case
        -- Se deu timeout e era verde, fica vermelha.
        temp.timeout & sinaleira_leste = verde : vermelho;
        -- Se não tem veículos no leste, fica vermelha.
        !veiculos_no_leste : vermelho;
        -- Se tem veículos no leste e nenhum sobre a ponte, fica verde.
        -- Contanto que seja a vez dessa sinaleira.
        vez_sinaleira = leste & veiculos_no_leste & !veiculos_sobre_ponte : verde;
        -- Default.
        TRUE : sinaleira_leste;
      esac;

---------------------------------------------------------------------------
-- PROPRIEDADES                                                          --
---------------------------------------------------------------------------
